# Specify the provider and access details
provider "aws" {
  region = "us-east-1"
}

resource random_string interview_id {
  length    = 6
  special   = false
  min_lower = 6
}

# Create a VPC to launch our instances into
resource "aws_vpc" "interview-vpc" {
  cidr_block = "10.0.0.0/16"

  tags = {
    Name = "interview-vpc-${random_string.interview_id.result}"
  }
}

# Create an internet gateway to give our subnet access to the outside world
resource "aws_internet_gateway" "default" {
  vpc_id = aws_vpc.interview-vpc.id
}

# Grant the VPC internet access on its main route table
resource "aws_route" "internet_access" {
  route_table_id         = aws_vpc.interview-vpc.main_route_table_id
  destination_cidr_block = "0.0.0.0/0"
  gateway_id             = aws_internet_gateway.default.id
}

# Create a subnet to launch our instances into
resource "aws_subnet" "default" {
  count = 2

  vpc_id                  = aws_vpc.interview-vpc.id
  cidr_block              = cidrsubnet(aws_vpc.interview-vpc.cidr_block, 1, count.index)
  map_public_ip_on_launch = true
}

# A security group for the ELB so it is accessible via the web
resource "aws_security_group" "elb" {
  name        = "terraform_example_elb-${random_string.interview_id.result}"
  description = "Used in the terraform"
  vpc_id      = aws_vpc.interview-vpc.id

  # HTTP access from anywhere
  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  # outbound internet access
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

# Our default security group to access
# the instances over SSH and HTTP
resource "aws_security_group" "default" {
  name        = "terraform_example-${random_string.interview_id.result}"
  description = "Used in the terraform"
  vpc_id      = aws_vpc.interview-vpc.id

  # SSH access from anywhere
  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  # HTTP access from the VPC
  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = [aws_vpc.interview-vpc.cidr_block]
  }

  # outbound internet access
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "aws_key_pair" "auth" {
  key_name   = "interview-key-${random_string.interview_id.result}"
  public_key = file(var.public_key_path)
}

data "aws_ami" "amazon-linux" {
  most_recent = true

  filter {
    name   = "name"
    values = ["amzn-ami-hvm-*-x86_64-gp2"]
  }

  filter {
    name   = "virtualization-type"
    values = ["hvm"]
  }

  owners = ["137112412989"] # Amazon - us-east-1
}
